# Maintainer: Chan Beom Park <cbpark@gmail.com>

_hkgname=threadscope
pkgname=haskell-threadscope
pkgver=0.2.7
pkgrel=1
pkgdesc="A graphical viewer for thread profile information generated by the Glasgow Haskell compiler (GHC)."
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc>=8.0.1'
         'haskell-cairo'
         'haskell-ghc-events>=0.4.2'
         'haskell-glib'
         'haskell-gtk2>=0.12'
         'haskell-mtl'
         'haskell-pango'
         'haskell-text')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('cc5653831252d55b3ba7506ea648e770b2c4489cdf4d78828f07dc24ea7ffdb6')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
